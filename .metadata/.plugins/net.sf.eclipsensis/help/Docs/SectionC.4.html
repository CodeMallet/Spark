<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns='http://www.w3.org/1999/xhtml'>

<head>
<title>Search in a string</title>
<meta name="generator" content="Halibut version 1.0 (NSIS Custom Build) xhtml-backend" />
<link rel="stylesheet" href="style.css" type='text/css' /><script language="JavaScript" type="text/javascript" src="chmlink.js"></script>
</head>

<body>
<p><a href='SectionC.3.html'>Previous</a> | <a href='Contents.html'>Contents</a> | <a href='SectionC.5.html'>Next</a></p>
<a name="C.4"></a><h2>C.4 Search in a string</h2>
<pre> ; StrStr
 ; input, top of stack = string to search for
 ;        top of stack-1 = string to search in
 ; output, top of stack (replaces with the portion of the string remaining)
 ; modifies no other variables.
 ;
 ; Usage:
 ;   Push &quot;this is a long ass string&quot;
 ;   Push &quot;ass&quot;
 ;   Call StrStr
 ;   Pop $R0
 ;  ($R0 at this point is &quot;ass string&quot;)

 Function StrStr
   Exch $R1 ; st=haystack,old$R1, $R1=needle
   Exch    ; st=old$R1,haystack
   Exch $R2 ; st=old$R1,old$R2, $R2=haystack
   Push $R3
   Push $R4
   Push $R5
   StrLen $R3 $R1
   StrCpy $R4 0
   ; $R1=needle
   ; $R2=haystack
   ; $R3=len(needle)
   ; $R4=cnt
   ; $R5=tmp
   loop:
     StrCpy $R5 $R2 $R3 $R4
     StrCmp $R5 $R1 done
     StrCmp $R5 &quot;&quot; done
     IntOp $R4 $R4 + 1
     Goto loop
 done:
   StrCpy $R1 $R2 &quot;&quot; $R4
   Pop $R5
   Pop $R4
   Pop $R3
   Pop $R2
   Exch $R1
 FunctionEnd
</pre>
<p><a href='SectionC.3.html'>Previous</a> | <a href='Contents.html'>Contents</a> | <a href='SectionC.5.html'>Next</a></p>

<hr />

<address>
</address>
</body>

</html>
